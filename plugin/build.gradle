import java.nio.file.Paths

plugins {
    id("org.jetbrains.kotlin.jvm")
    id("maven-publish")
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-compiler-embeddable:$KOTLIN_VERSION"
    compileOnly "io.arrow-kt:compiler-plugin:$ARROW_META_VERSION"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "$JVM_TARGET_VERSION"
    }
}

// Add new content to the default jar artifact: Arrow Meta Compiler Plugin except META-INF file to use the new one
jar {
    from (
        zipTree(sourceSets.main.compileClasspath.find {
            it.absolutePath.contains(Paths.get("arrow-kt","compiler-plugin").toString())
        })
    ) {
        exclude 'META-INF/services/org.jetbrains.kotlin.compiler.plugin.ComponentRegistrar'
    }
}

publishing {
    repositories {
        maven {
            name = "Artifactory"
            url = uri(System.getenv("ARTIFACTORY_CONTEXT_URL"))
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("ARTIFACTORY_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("ARTIFACTORY_PASSWORD")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}