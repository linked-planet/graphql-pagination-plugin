import java.nio.file.Paths

plugins {
    id("org.jetbrains.kotlin.jvm")
    id("maven-publish")
}

dependencies {
    compileOnly("org.jetbrains.kotlin:kotlin-compiler-embeddable:$KOTLIN_VERSION")
    compileOnly("io.arrow-kt:compiler-plugin:$ARROW_META_VERSION")
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "$JVM_TARGET_VERSION"
    }
}

// Add new content to the default jar artifact: Arrow Meta Compiler Plugin except META-INF file to use the new one
jar {
    from (
        zipTree(sourceSets.main.compileClasspath.find {
            it.absolutePath.contains(Paths.get("arrow-kt","compiler-plugin").toString())
        })
    ) {
        exclude "META-INF/services/org.jetbrains.kotlin.compiler.plugin.ComponentRegistrar"
    }
}

publishing {
    repositories {
        maven {
            name = "Artifactory"
            url = uri(System.getenv("ARTIFACTORY_CONTEXT_URL") ?: "/tmp/release")
            credentials {
                username = project.findProperty("artifactory.user") ?: System.getenv("ARTIFACTORY_USERNAME")
                password = project.findProperty("artifactory.key") ?: System.getenv("ARTIFACTORY_PASSWORD")
            }
        }
    }
    publications {
        artifactory(MavenPublication) {
            from(components.java)
            artifactId = parent.name
            groupId = parent.group
            version = System.getenv("RELEASE_NAME") ?: "0.0.3-SNAPSHOT"
            pom {
                name = "graphql-pagination-plugin"
            }
        }
    }
}